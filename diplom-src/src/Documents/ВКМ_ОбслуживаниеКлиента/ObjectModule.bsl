
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ,Режим)
	
	// БондаренкоДМ ++
	Если Не ВКМ_ПроверкаДокумента() Тогда
		ОбщегоНазначения.СообщитьПользователю("Выбран договор с типом не абонентская плата или договор не актуален");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПроцентОтРабот = ВКМ_ПроцентОтРабот(Специалист, Дата);
	
	Если ПроцентОтРабот = Неопределено Тогда
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(
				"Сотруднику %1 не указан процент от работ в условиях оплаты.", Специалист));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ОбслуживаниеКлиента.Клиент,
	|	ВКМ_ОбслуживаниеКлиента.Договор,
	|	СУММА(ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.ЧасыКОплатеКлиенту) КАК КоличествоЧасов,
	|	СУММА(ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.ЧасыКОплатеКлиенту *
	|		ДоговорыКонтрагентов.ВКМ_СтоимостьЧасаРабот) КАК СуммаКОплате
	|ИЗ
	|	Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВКМ_ОбслуживаниеКлиента.ВыполненныеРаботы КАК
	|			ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы
	|		ПО ВКМ_ОбслуживаниеКлиента.Ссылка = ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО ВКМ_ОбслуживаниеКлиента.Договор = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	ВКМ_ОбслуживаниеКлиента.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ОбслуживаниеКлиента.Клиент,
	|	ВКМ_ОбслуживаниеКлиента.Договор";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		// Регистр ВКМ_ВыполненныеКлиентуРаботы
		Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Договор = Выборка.Договор;
		Движение.Клиент = Выборка.Клиент;
		Движение.КоличествоЧасов = Выборка.КоличествоЧасов;
		Движение.СуммаКОплате = Выборка.СуммаКОплате;
		
		// Регистр ВКМ_ВыполненныеСотрудникомРаботы
		Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
		Движение.Период = Дата;
		Движение.Сотрудник = Специалист;
		Движение.ЧасовКОплате = Выборка.КоличествоЧасов;
		Движение.СуммаКОплате = Выборка.СуммаКОплате * ПроцентОтРабот / 100;
		
	КонецЦикла;
	// БондаренкоДМ --
			
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВКМ_ПроверкаДокумента()
	
	// Проверяем, что выбран договор с типом абонентская плата и что дата документа лежит между датой начала и датой
	// окончания действия договора
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОбслуживаниеКлиента.Договор.ВидДоговора,
		|	ВКМ_ОбслуживаниеКлиента.Договор.ВКМ_ДатаНачалаДействияДоговора,
		|	ВКМ_ОбслуживаниеКлиента.Договор.ВКМ_ДатаОкончанияДействияДоговора КАК ВКМ_ДоговорВКМ_ДатаОкончанияДействияДоговора
		|ИЗ
		|	Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
		|ГДЕ
		|	ВКМ_ОбслуживаниеКлиента.Ссылка = &Ссылка
		|	И
		|		ВКМ_ОбслуживаниеКлиента.Договор.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание)
		|	И ВКМ_ОбслуживаниеКлиента.Дата
		|		МЕЖДУ ВКМ_ОбслуживаниеКлиента.Договор.ВКМ_ДатаНачалаДействияДоговора И КОНЕЦПЕРИОДА(ВКМ_ОбслуживаниеКлиента.Договор.ВКМ_ДатаОкончанияДействияДоговора,
		|		День)";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Запрос.Выполнить();

	Если Результат.Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

Функция ВКМ_ПроцентОтРабот(Сотрудник, Дата);

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот КАК ПроцентОтРабот
	|ИЗ
	|	РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Дата, Сотрудник = &Сотрудник) КАК
	|		ВКМ_УсловияОплатыСотрудниковСрезПоследних";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда 
		Возврат Неопределено;
	Иначе  
		Возврат Выборка.ПроцентОтРабот;
	КонецЕсли;
	
КонецФункции


#КонецОбласти

#КонецЕсли
