
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Создать(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Период) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не выбран период");
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Интервал = 1;
	
	ДлительнаяОперация = ВыполнитьТяжелуюОперациюНаСервере();
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОповещениеОЗавершении", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);

КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОповещениеОЗавершении(Результат, ДополнительныеПараметры) Экспорт
	
	МассивДляТЧ = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	ДобавитьСтрокувВТЧОбработки(МассивДляТЧ);
	ОчиститьСообщения();
	ОбщегоНазначенияКлиент.СообщитьПользователю("Обработка выполнена");
	
КонецПроцедуры 

&НаСервере
Функция ВыполнитьТяжелуюОперациюНаСервере()
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор,
	"Обработки.ВКМ_МассовоеСозданиеАктов.ТяжелаяОперация", Объект.Период);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаСервере
Процедура ДобавитьСтрокувВТЧОбработки(МассивДляТЧ)
	
	ТабЧасть = Объект.ДоговорыИРеализации;
	ТабЧасть.Очистить();
	
	Для Каждого Элемент Из МассивДляТЧ Цикл
		
		Обработки.ВКМ_МассовоеСозданиеАктов.ДобавитьСтрокуВТЧ(Элемент.Договор, Элемент.Реализация, ТабЧасть); 
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти
